<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1">
    <meta name="viewport" content="width=device-width, minimal-ui, viewport-fit=cover, initial-scale=1.0, maximum-scale=1.0, minimum-scale=1.0, user-scalable=no">
    <link rel="icon" type="image/png" href="../assets/favicon.png">
    <link href="https://fonts.googleapis.com/css?family=Staatliches&display=swap" rel="stylesheet">
    <title>06. Audio Reactive</title>
    <link href="../assets/style.css" rel="stylesheet">
    <link href="player.css" rel="stylesheet">
</head>
<body>
    <h1>06. Audio Reactive (ft Caravan Palace)</h1>
    		<!-- div that will hold our WebGL canvas -->
    <div id="canvas"></div>

    <!-- div used to create our plane -->
    <div class="plane">
        <!-- image that will be used as a texture by our plane -->
        <video autoplay loop muted playsinline>
            <source src="motion.mp4" type="video/mp4" />
        </video>
        <div class="content">
            <h3><span>Caravan Palace</span></h3>
            <h2><span>Plume</span></h2>
        </div>
        
    </div>

   <!-- vertex shader -->
    <script id="plane-vs" type="x-shader/x-vertex">
        #ifdef GL_ES
        precision mediump float;
        #endif

        // those are the mandatory attributes that the lib sets
        attribute vec3 aVertexPosition;
        attribute vec2 aTextureCoord;

        // those are mandatory uniforms that the lib sets and that contain our model view and projection matrix
        uniform mat4 uMVMatrix;
        uniform mat4 uPMatrix;

        // our texture matrix uniform (this is the lib default name, but it could be changed)
        uniform mat4 uTextureMatrix0;

        // if you want to pass your vertex and texture coords to the fragment shader
        varying vec3 vVertexPosition;
        varying vec2 vTextureCoord;

        void main() {
            vec3 vertexPosition = aVertexPosition;

            gl_Position = uPMatrix * uMVMatrix * vec4(vertexPosition, 1.0);

            // set the varyings
            // thanks to the texture matrix we will be able to calculate accurate texture coords
            // so that our texture will always fit our plane without being distorted
            vTextureCoord = (uTextureMatrix0 * vec4(aTextureCoord, 0.0, 1.0)).xy;
            vVertexPosition = vertexPosition;
        }
    </script>
    <!-- fragment shader -->
    <script id="plane-fs" type="x-shader/x-fragment">
        #ifdef GL_ES
        precision mediump float;
        #endif
        // get our varyings
        varying vec3 vVertexPosition;
        varying vec2 vTextureCoord;

        // our texture sampler (default sampler name)
        uniform sampler2D uSampler0;
        uniform float uTime;
        uniform float uIntensity;
        uniform vec2 uMouse;
        uniform mat4 uTextureMatrix0;

        uniform float uKick;
        uniform float uVolume;
        uniform float uBeat;
        
        const float pi = 3.14159;

        float remap( float minval, float maxval, float curval )
        {
            return ( curval - minval ) / ( maxval - minval );
        } 

        mat2 rotate2d(float _angle){
            return mat2(cos(_angle),-sin(_angle),
                        sin(_angle),cos(_angle));
        }

        
        vec3 permute(vec3 x) { return mod(((x*34.0)+1.0)*x, 289.0); }

        float snoise(vec2 v){
            const vec4 C = vec4(0.211324865405187, 0.366025403784439,
                    -0.577350269189626, 0.024390243902439);
            vec2 i  = floor(v + dot(v, C.yy) );
            vec2 x0 = v -   i + dot(i, C.xx);
            vec2 i1;
            i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
            vec4 x12 = x0.xyxy + C.xxzz;
            x12.xy -= i1;
            i = mod(i, 289.0);
            vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
            + i.x + vec3(0.0, i1.x, 1.0 ));
            vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy),
                dot(x12.zw,x12.zw)), 0.0);
            m = m*m ;
            m = m*m ;
            vec3 x = 2.0 * fract(p * C.www) - 1.0;
            vec3 h = abs(x) - 0.5;
            vec3 ox = floor(x + 0.5);
            vec3 a0 = x - ox;
            m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );
            vec3 g;
            g.x  = a0.x  * x0.x  + h.x  * x0.y;
            g.yz = a0.yz * x12.xz + h.yz * x12.yw;
            return 130.0 * dot(m, g);
        }

        const float iteration = 0.1;
        
        void main() {
            // map our texture with the original texture coords
            vec2 uv = vTextureCoord;

            uv += vec2(1.0,1.0) * (snoise(vec2(uv.y + uTime * 0.5 , uv.x + uTime * 0.5) * 10.0)) * 0.1 * pow(uVolume,2.0) * distance(uv,vec2(0.5));

            vec2 dir = uv - 0.5;
            float radius = length(dir);

            float strength = uKick * 0.3;
            float resizeRatio = abs((strength - 1.0) * 1.8);

            vec2 uvR = (uv - dir * radius * strength * 1.3 - 0.5) + 0.5;
            vec2 uvG = (uv - dir * radius * strength * 1.2 - 0.5) + 0.5;
            vec2 uvB = (uv - dir * radius * strength - 0.5) + 0.5;

            vec2 scope = vec2(1.0,1.0);

            vec4 R = texture2D(uSampler0, fract(uvR * scope));
            vec4 G = texture2D(uSampler0, fract(uvG * scope));
            vec4 B = texture2D(uSampler0, fract(uvB * scope));

            vec4 chroma = vec4(R.r,G.g,B.b,1.0);

            
            float test = 0.0;
            for(float x = -0.5; x<= 0.5; x+= iteration){
                for(float y = -0.5; y<= 0.5; y+= iteration){
                    vec4 light = texture2D(uSampler0, fract((uvR + vec2(x,y) * 0.005) * scope) );
                    float white = 0.2126 * light.r + 0.7152 * light.g + 0.0722 * light.b;
                    white = smoothstep(0.02,0.8,white);
                    test += white * 0.01;
                } 
            }

            test *= 1.0 * uBeat;


            gl_FragColor = chroma + vec4(test,test,test, 1.0);
        }
    </script>
    <audio id="audio" preload="none" controls='true'>
        <source></source>
    </audio>
    <div class="cursor">Play</div>
    <script src="../assets/curtains.min.js"></script>
    <script type="module" src="main.js"></script>
</body>
</html>